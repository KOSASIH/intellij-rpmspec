{
  parserClass="com.carbonblack.intellij.rpmspec.parser.RpmSpecParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="RpmSpec"
  psiImplClassSuffix="Impl"
  psiPackage="com.carbonblack.intellij.rpmspec.psi"
  psiImplPackage="com.carbonblack.intellij.rpmspec.psi.impl"

  elementTypeHolderClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTypes"
  elementTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecElementType"
  tokenTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTokenType"

    tokens = [
        LBRACE            =  '{'
        RBRACE            =  '}'
        LPAREN            =  '('
        RPAREN            =  ')'
        COLON             =  ':'
        PERCENT           =  '%'
        ESCAPED_PERCENT   =  '%%'
        QUESTION_MARK     =  '?'
    ]
}

rpmSpecFile ::= EOL* packageBody item_*

private item_ ::= (packageSection|descriptionSection|changelogSection|otherSections)

private anyWord ::= (IDENTIFIER|FLOAT_LITERAL|INTEGER_LITERAL|CODE|':'|'%' '%')
private wordOrMacro ::= (fullMacro|anyWord)

macroDefinition ::= (global|define) macro (wordOrMacro|'('|')')+ {pin=1}
macroUndefine ::= undefine macro {pin=1}
fullMacro ::= '%' ('{' '?'? macro (':'? wordOrMacro+)? '}'|macro ('(' anyWord ')')?) anyWord* {pin=1}

packageSection ::= package wordOrMacro? packageBody {pin=1}
packageBody ::= (COMMENT|EOL|<<IfExpr packageBody>>|fullMacro|macroDefinition|macroUndefine|tag)*

meta IfExpr ::= if wordOrMacro+ <<param>> item_* <<ElseBranch <<param>>>>? endif {pin=1}
meta ElseBranch ::= else <<param>> item_*

tag ::= PREAMBLE_TAG ':' tagValue {pin=2}
tagValue ::= (wordOrMacro|'('|')')+

meta genericSection ::= <<param>> wordOrMacro? genericBody
genericBody ::= (COMMENT|EOL|<<IfExpr genericBody>>|macroDefinition|macroUndefine|(wordOrMacro|'{'|'}'|'?'|'('|')'|other))*

descriptionSection ::= <<genericSection description>>
changelogSection ::= <<genericSection changelog>>
otherSections ::= <<genericSection (prep|build|install|files)>>

macro ::= IDENTIFIER {implements="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElement"
                      mixin="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElementImpl"}


private other ::= ('%%'|pre|BOOL_LITERAL|post|clean|check|preun|postun|pretrans|posttrans)
