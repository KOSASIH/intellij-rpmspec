{
  parserClass="com.carbonblack.intellij.rpmspec.parser.RpmSpecParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="RpmSpec"
  psiImplClassSuffix="Impl"
  psiPackage="com.carbonblack.intellij.rpmspec.psi"
  psiImplPackage="com.carbonblack.intellij.rpmspec.psi.impl"

  elementTypeHolderClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTypes"
  elementTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecElementType"
  tokenTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTokenType"

    tokens = [
        LBRACE            =  '{'
        RBRACE            =  '}'
        LPAREN            =  '('
        RPAREN            =  ')'
        COLON             =  ':'
        PERCENT           =  '%'
        ESCAPED_PERCENT   =  '%%'
        QUESTION_MARK     =  '?'
        ASTERISK          =  '*'
        MINUS             =  '-'
    ]
}

rpmSpecFile ::= EOL* packageBody section*

private section ::= (packageSection|descriptionSection|changelogSection|otherSections)

private anyWord ::= (IDENTIFIER|BOOL_LITERAL|FLOAT_LITERAL|INTEGER_LITERAL|CODE|':'|'%%')
private wordOrMacro ::= (fullMacro|anyWord)

macroDefinition ::= (global|define) macro (wordOrMacro|'('|')')+ {pin=1}
macroUndefine ::= undefine macro {pin=1}
fullMacro ::= '%' ('{' '?'? macro (':'? wordOrMacro+)? '}'|macro ('(' anyWord ')')?) anyWord* {pin=1}

packageSection ::= package wordOrMacro? packageBody {pin=1}
packageBody ::= (COMMENT|EOL|<<ifExpr packageBody>>|fullMacro|macroDefinition|macroUndefine|tag)*

meta ifExpr ::= if wordOrMacro+ EOL <<param>> section* <<elseBranch <<param>>>>? endIfExpr {pin=1}
meta elseBranch ::= else <<param>> section* {pin=1}
endIfExpr ::= endif

tag ::= PREAMBLE_TAG ':' tagValue {pin=2}
tagValue ::= (wordOrMacro|'('|')')+

meta genericSection ::= <<param>> wordOrMacro? genericBody {pin=1}
genericBody ::= (COMMENT|EOL|<<ifExpr genericBody>>|macroDefinition|macroUndefine|(wordOrMacro|'{'|'}'|'?'|'('|')'|'*'|'-'))*

descriptionSection ::= <<genericSection description>>
otherSections ::= <<genericSection (pre|prep|build|install|files|post|clean|check|preun|postun|pretrans|posttrans)>>

changelogSection ::= changelog changelogBody {pin=1}
changelogBody ::= (EOL|changelogItem)*
changelogItem ::= (changelogHeader EOL changelogEntry*)
changelogHeader ::= '*' (wordOrMacro|'{'|'}'|'?'|'('|')')+ {pin=1}
changelogEntry ::= '-' (wordOrMacro|'{'|'}'|'?'|'('|')'|EOL)+ {pin=1}

macro ::= IDENTIFIER {implements="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElement"
                      mixin="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElementImpl"}
