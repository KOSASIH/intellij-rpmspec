{
  parserClass="com.carbonblack.intellij.rpmspec.parser.RpmSpecParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="RpmSpec"
  psiImplClassSuffix="Impl"
  psiPackage="com.carbonblack.intellij.rpmspec.psi"
  psiImplPackage="com.carbonblack.intellij.rpmspec.psi.impl"

  elementTypeHolderClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTypes"
  elementTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecElementType"
  tokenTypeClass="com.carbonblack.intellij.rpmspec.psi.RpmSpecTokenType"

    tokens = [
        LBRACE            =  '{'
        RBRACE            =  '}'
        LPAREN            =  '('
        RPAREN            =  ')'
        COLON             =  ':'
        PERCENT           =  '%'
        QUESTION_MARK     =  '?'
    ]
}

rpmSpecFile ::= packageBody item_*

private item_ ::= (packageSection|descriptionSection|changelogSection|otherSections)

private anyWord ::= (IDENTIFIER|FLOAT_LITERAL|INTEGER_LITERAL|CODE|':'|'%' '%')
private wordOrMacro ::= (fullMacro|anyWord)

macroDefinition ::= (global|define) macro (wordOrMacro|'('|')')+
macroUndefine ::= undefine macro
fullMacro ::= '%' ('{' '?'? macro (':'? wordOrMacro+)? '}'|macro ('(' anyWord ')')?) anyWord*

packageSection ::= package wordOrMacro? packageBody
packageBody ::= (COMMENT|<<IfExpr packageBody>>|fullMacro|macroDefinition|macroUndefine|tag)*

meta IfExpr ::= if wordOrMacro+ <<param>> item_* <<ElseBranch <<param>>>>? endif
meta ElseBranch ::= else <<param>> item_*

tag ::= PREAMBLE_TAG ':' tagValue
tagValue ::= (wordOrMacro|'('|')')+

meta genericSection ::= <<param>> wordOrMacro? genericBody
genericBody ::= (COMMENT|<<IfExpr genericBody>>|macroDefinition|macroUndefine|(wordOrMacro|'{'|'}'|'?'|'('|')'|other))*

descriptionSection ::= <<genericSection description>>
changelogSection ::= <<genericSection changelog>>
otherSections ::= <<genericSection (prep|build|install|files)>>

macro ::= IDENTIFIER {implements="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElement"
                      mixin="com.carbonblack.intellij.rpmspec.psi.RpmSpecMacroElementImpl"}


private other ::= (pre|BOOL_LITERAL|post|clean|check|preun|postun|pretrans|posttrans)
